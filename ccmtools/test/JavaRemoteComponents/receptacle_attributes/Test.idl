/**
 * CCM Tools test case: facet_module_attributes
 *
 * This test case checks attribute access with
 * different attribute types.
 **/

module world 
{
  enum Color {red, green, blue, black, orange};
  
  struct Person 
  {
    long id;
    string name;
  };
  
  struct Address 
  {
    string street;
    long   number;
    Person resident;
  };
  
  typedef long time_t;
  
  typedef sequence<long> LongList;
  typedef sequence<string> StringList;
  typedef sequence<Person> PersonList;

  module europe 
  {

    interface BasicTypeInterface
    {
      attribute short short_value;
      attribute long long_value;
      attribute unsigned short ushort_value;
      attribute unsigned long ulong_value;
      attribute float float_value;
      attribute double double_value;
      attribute char char_value;
      attribute string string_value;
      attribute boolean boolean_value;
      attribute octet octet_value;
    };
    
    
    interface UserTypeInterface
    {
      attribute Color color_value;
      attribute Person person_value;
      attribute Address address_value;
  
      attribute LongList longList_value;
      attribute StringList stringList_value;
      attribute PersonList personList_value;
      
      attribute time_t time_t_value;
    };
    

    module austria 
    {      
      
      component Test
      {
	uses BasicTypeInterface basicType;
	uses UserTypeInterface userType;
      };
      
      home TestHome manages Test
      {
      };
      
    }; // end of module austria
  }; // end of module europe
}; // end of module world

