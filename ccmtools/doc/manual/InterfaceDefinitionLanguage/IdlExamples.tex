%==============================================================================
\chapter{IDL Examples}
%==============================================================================

%------------------------------------------------------------------------------
\section{Interfaces with Basic IDL Types}
%------------------------------------------------------------------------------

\begin{small}
\begin{verbatim}
module world 
{
    interface VoidTypeInterface 
    {
        void f1(in long p1);
        long f2();
    };

    module europe 
    {
        interface BasicTypeInterface 
        {
            short f1(in short p1, inout short p2, out short p3);

            long  f2(in long p1, inout long p2, out long p3);

            unsigned short f3(in unsigned short p1, 
                              inout unsigned short p2, 
                              out unsigned short p3);

            unsigned long f4(in unsigned long p1, 
                             inout unsigned long p2, 
                             out unsigned long p3);

            float f5(in float p1, inout float p2, out float p3);

            double f6(in double p1, inout double p2, out double p3);

            char f7(in char p1, inout char p2, out char p3);

            string f8(in string p1, inout string p2, out string p3);

            boolean f9(in boolean p1, inout boolean p2, out boolean p3);

            octet f10(in octet p1, inout octet p2, out octet p3);
        };

    }; // end of module europe
}; // end of module world
\end{verbatim}
\end{small}


%------------------------------------------------------------------------------
\section{Interface with User--Defined IDL Types}
%------------------------------------------------------------------------------

\begin{small}
\begin{verbatim}
module world 
{
    enum Color 
    {
        red, 
        green, 
        blue, 
        black, 
        orange
    };
  
    struct Person 
    {
        long id;
        string name;
    };
  
    typedef long time_t;
    typedef sequence<long> LongList;
    typedef sequence<string> StringList;
    typedef sequence<Person> PersonList;

    module europe 
    {
        module austria 
        {
            struct Address 
            {
                string street;
                long   number;
                world::Person resident;
            };
            
            interface UserTypeInterface 
            {
                Color f1(in Color p1, inout Color p2, out Color p3);

                world::Person f2(in world::Person p1, 
                                 inout world::Person p2, 
                                 out world::Person p3);

                Address f3(in Address p1, 
                           inout Address p2, 
                           out Address p3);

                LongList f4(in LongList p1, 
                            inout LongList p2, 
                            out LongList p3);

                StringList f5(in StringList p1, 
                              inout StringList p2, 
                              out StringList p3);

                PersonList f6(in PersonList p1, 
                              inout PersonList p2, 
                              out PersonList p3);

                time_t f7(in time_t t1, 
                          inout time_t t2, 
                          out time_t t3);
            };
      
        }; // end of module austria
    }; // end of module europe
}; // end of module world
\end{verbatim}
\end{small}


%------------------------------------------------------------------------------
\section{Interface with Constants}
%------------------------------------------------------------------------------
\begin{small}
\begin{verbatim}
module world
{
    interface ConstantsInterface
    {
        const boolean BOOLEAN_CONST = TRUE;
        const octet OCTET_CONST = 255;
        const short SHORT_CONST = -7+10;
        const unsigned short USHORT_CONST = 7;
        const long LONG_CONST = -7777;
        const unsigned long ULONG_CONST = 7777;
        const char CHAR_CONST = 'c';
        const string STRING_CONST = "1234567890";  
        const float FLOAT_CONST = 3.14;
        const double DOUBLE_CONST = 3.1415926*2.0;
    };
}; // end of module world
\end{verbatim}
\end{small}


%------------------------------------------------------------------------------
\section{Interfaces with Attributes}
%------------------------------------------------------------------------------
\begin{small}
\begin{verbatim}
module world 
{
  module europe 
  {
      interface BasicTypeAttributeInterface
      {
          attribute short shortValue;
          attribute long longValue;
          attribute unsigned short ushortValue;
          attribute unsigned long ulongValue;
          attribute float floatValue;
          attribute double doubleValue;
          attribute char charValue;
          attribute string stringValue;
          attribute boolean booleanValue;
          attribute octet octetValue;
      };
    
      module austria
      {    
          interface UserTypeAttributeInterface
          {
              attribute Color colorValue;
              attribute Person personValue;
              attribute Address addressValue;
  
              attribute LongList longListValue;
              attribute StringList stringListValue;
              attribute PersonList personListValue;
      
              attribute time_t time_tValue;
          };
      }; // end of module austria
  }; // end of module europe
}; // end of module world
\end{verbatim}
\end{small}


%------------------------------------------------------------------------------
\section{Interface with Exceptions}
%------------------------------------------------------------------------------
\begin{small}
\begin{verbatim}
module world 
{
  module europe 
  {
    module austria 
    {      
      struct ErrorInfo
      {
          long code;
          string message;
      };
      
      typedef sequence<ErrorInfo> ErrorInfoList;
      
      exception ErrorException
      {
          ErrorInfoList info;
      };
      
      exception SuperError{};

      exception FatalError
      {
          string what;
      };
      
      interface ExceptionInterface
      {
          long foo(in string msg)
            raises (ErrorException, SuperError, FatalError);
      };

    }; // end of module austria
  }; // end of module europe
}; // end of module world
\end{verbatim}
\end{small}






%------------------------------------------------------------------------------
\section{Interfaces with Inheritance}
%------------------------------------------------------------------------------
\begin{small}
\begin{verbatim}
module europe
{
    interface SuperType1
    {
        attribute long attr1;
        long op1(in string str);
    };
}; // end of module europe

module america
{
    interface SuperType2
    {
        attribute long attr2;
        long op2(in string str);
    };

    interface SubType 
      : europe::SuperType1, SuperType2 
    {
        attribute long attr3;
        long op3(in string str);
    };
}; // end of module america
\end{verbatim}
\end{small}


%------------------------------------------------------------------------------
\section{Component with Facets}
%------------------------------------------------------------------------------
\begin{small}
\begin{verbatim}
module world 
{
  module europe 
  {
    module austria 
    {
        component Test
        {
            provides world::VoidTypeInterface voidType;
            provides world::europe::BasicTypeInterface basicType;
            provides world::europe::austria::UserTypeInterface userType;
        };
      
        home TestHome manages Test
        {
        };

    }; // end of module austria
  }; // end of module europe
}; // end of module world
\end{verbatim}
\end{small}
